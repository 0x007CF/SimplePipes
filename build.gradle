plugins {
    id 'fabric-loom' version '0.2.5-SNAPSHOT'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
}

archivesBaseName = "SimplePipes"
version = "0.2.0-pre.4"

minecraft {
}

configurations {
    nest {}
}

dependencies {
    minecraft "com.mojang:minecraft:1.14.4"
    mappings "net.fabricmc:yarn:1.14.4+build.9"
    modCompile "net.fabricmc:fabric-loader:0.49.8+gui.55"

    //Fabric api
    modCompile "net.fabricmc.fabric-api:fabric-api:0.3.1+build.208"

    // Silk API
    // modCompile "io.github.prospector.silk:SilkAPI:1.2.3-38"

    modApi "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
    modApi "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    modApi "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

    include "alexiil.mc.lib:libblockattributes-core:$libblockattributes_version"
    include "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    include "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

    // Misc
    compile "com.google.code.findbugs:jsr305:3.0.1"
    testCompile "junit:junit:4.12"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
        expand project.properties
        /*expand "libblockattributes_version": ext.libblockattributes_version
        expand "libnetworkstack_version": "$libnetworkstack_version"
        expand "libmultipart_version": "$libmultipart_version"*/
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }

    // FIXME: Apparently we can wait for fabric-loom to provide support for this.
    from (configurations.nest.artifacts)
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
}

// #####################
//
// Extra jar section
//
// #####################

apply from: "extra_jar_def.gradle"

// Variables (that should be changed)
ext.mainName = "simplepipes"
ext.mavenGroupId = "alexiil.mc.mod";

def allIncludes = [
    "assets/simple_pipes/icon.png"
];

def jarDeps = [ 
    ["alexiil.mc.lib", "libblockattributes-all", "$libblockattributes_version"],
    ["alexiil.mc.lib", "libnetworkstack-base", "$libnetworkstack_version"],
    ["alexiil.mc.lib", "libmultipart-base", "$libmultipart_version"]
];

generateJar("base", ["**"], [], true);
generateJar("all", allIncludes, [], false, [], ["base"], [], jarDeps);
